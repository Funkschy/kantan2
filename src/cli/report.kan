import "io";

import "config";

import "../std/str";
import "../std/dbg";
import "../std/cdeps";

import "../error" as e;

import "../source/span";
import "../source/file" as sf;
import "../source/modmap" as mods;
import "../source/position" as pos;

// This module is used to report errors to the user. Depending on the value of 'format', the output
// will either be in plain text or json. The format value cannot be changed during runtime

let error_prefix = "error: ";
// undefined for global variables means 0, which in turn means the first enum variant
// this is pretty hacky, but works for now
let format: config.ErrorOutputFormat = undefined;
// see above
let print_comma: bool = undefined;
let modmap: *mods.Map = null;

// this is used for indenting error messages. Therefore the length of this string is also the
// maximimum intendation for errors
let spaces = "                ";
// errors in human-readable mode will be printed immediately, but for json, we might need some
// formatting, so it's easier to just write to a buffer
let json_error_buffer: str.String = undefined;

def init(output_format: config.ErrorOutputFormat, mods: *mods.Map) {
    format = output_format;
    modmap = mods;

    if format == config.ErrorOutputFormat.JSON {
        json_error_buffer = str.from("{\"errors\": [");
    }
}

def finish() {
    if format == config.ErrorOutputFormat.JSON {
        json_error_buffer.push(str.view("]}", 2));
        io.printf("%s\n", json_error_buffer.cstring());
        json_error_buffer.free();
    }
}


type ReportElement struct {
    _type: string,
    file: str.View,
    content: str.View,
    line: pos.LineInfo,
    first_note: *e.ErrorNote
}

def report_error(content: str.View, span: span.Span, note: *e.ErrorNote): ReportElement {
    return report_element("error", false, content, span, note);
}

def report_note(note: *e.ErrorNote): ReportElement {
    return report_element("note", note.without_position, note.message, note.span, note.next);
}

def report_error_opts(
    file: str.View,
    content: str.View,
    line: pos.LineInfo,
    first_note: *e.ErrorNote
): ReportElement {
    return ReportElement {
        _type: "error",
        file: file,
        content: content,
        line: line,
        first_note: first_note
    };
}

def report_element(
    _type: string,
    without_position: bool, // ignore the file + span
    content: str.View,
    span: span.Span,
    note: *e.ErrorNote
): ReportElement {
    if without_position {
        return ReportElement {
            _type: _type,
            file: str.view("", 0),
            content: content,
            line: pos.empty_line_info(),
            first_note: note
        };
    }

    let mod = modmap.get_mod_from_span(span);
    dbg.assert_fmt(mod != null, "could not get module for %s", _type);
    let line_info = mod.file.line_info(span);

    return ReportElement {
        _type: _type,
        file: mod.file.absolute_path().as_view(),
        content: content,
        line: line_info,
        first_note: note
    };
}

def print_spanned(line_info: pos.LineInfo, offset: u32) {
    io.printf("%.*s  | ", offset, spaces);
    let s = line_info.span;

    let ptr = s.start;
    let len = s.len();
    let num_lines = 1;
    while (ptr - s.start) as usize < len {
        let c = *ptr;
        io.putchar(c as i32);
        if c == '\n' {
            io.printf("%.*s  | ", offset, spaces);
            num_lines += 1;
        }
        ptr = ptr + 1;
    }

    io.putchar('\n');

    if num_lines == 1 {
        io.printf("%.*s  | ", offset, spaces);
        for let i: u32 = 1; i < line_info.start.col; i += 1 {
            io.putchar(' ');
        }
        io.printf("^\n");
    }
}

def (elem: *ReportElement) print() {
    if format == config.ErrorOutputFormat.HumanReadable {
        elem.print_offset(0);
    } else {
        elem.print_json();
    }
}

def (elem: *ReportElement) print_offset(offset: u32) {
    io.printf(
        "\n%.*s%s: %.*s\n%.*s",
        offset, spaces,
        elem._type,
        elem.content.len, elem.content.data,
        offset, spaces
    );

    // a file.len of 0 means, that we don't want to print the positional information
    if elem.file.len > 0 {
        io.printf(
            "--> %.*s:%u:%u\n",
            elem.file.len, elem.file.data,
            elem.line.start.lnr, elem.line.start.col
        );

        if !elem.line.is_empty() {
            print_spanned(elem.line, offset);
        }
    }

    let note = elem.first_note;
    if note != null {
        report_note(note).print_offset(2);
    }
}

def (elem: *ReportElement) print_json() {
    if print_comma {
        json_error_buffer.push(str.view(",", 1));
    }
    print_comma = true;

    // this is just way easier to write/read than pushing each field individually
    let len: usize = 0;
    let value = cdeps.l_format_str(
        &len,
        "{\"type\": \"%s\", \"file\": \"%.*s\", \"content\": \"%.*s\", \"lnr\": %u, \"col\": %u",
        elem._type,
        elem.file.len, elem.file.data,
        elem.content.len, elem.content.data,
        elem.line.start.lnr, elem.line.start.col
    );
    json_error_buffer.push(str.view(value, len));
    delete value;

    let note = elem.first_note;
    if note != null {
        // don't print a comma in front of the first note
        print_comma = false;
    }

    json_error_buffer.push(str.view_from(", \"notes\": ["));
    defer json_error_buffer.push(str.view("]}", 2));

    while note != null {
        let element = report_note(note);
        // since we don't want to traverse this recursively, we just set the next note to null
        element.first_note = null;
        element.print_json();
        note = note.next;
        print_comma = true;
    }
}

def print_simple(msg: str.View) {
    if format == config.ErrorOutputFormat.HumanReadable {
        io.printf("%s%.*s\n", error_prefix, msg.len, msg.data);
    } else {
        report_error_opts(str.view_from(""), msg, pos.empty_line_info(), null).print();
    }
}

def print_file_error(file: *sf.SourceFile, note: *e.ErrorNote) {
    let msg = file.error_msg();
    // the msg already contains the filename, so we set path to an empty view in order to not
    // print the filename twice
    report_error_opts(str.view("", 0), msg, pos.empty_line_info(), note).print();
}

def print_error(err: *e.Error) {
    let msg = err.to_string();
    defer msg.free();

    report_error(msg.view(), err.span, err.note).print();
}
