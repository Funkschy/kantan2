import ":std/str";
import ":std/dbg";

import ":util";
import ":error" as e;
import ":cli/report";
import ":source/span";
import ":ast/expr" as ast;

import "ctx";
import "types" as ty;

// this is relevant for the lookup of Ident and Access expressions
// after successfully inferring the type of such an expression, the TyResult will contain the
// type of scope, that the ident was resolved in
type FoundIn enum {
    // Stuff that wasn't found in any scope, e.g. literals
    None,
    // an imported item
    AnotherModule,
    // types declared in the current module
    Types,
    // declarations in the current module (signature or template)
    Declarations,
    // a local variable
    LocalVariables
}

type TyResult struct {
    found_in: FoundIn,
    ty: *ty.Type
}

def ok(found_in: FoundIn, ty: *ty.Type): TyResult {
    return TyResult { found_in: found_in, ty: ty };
}

def error(): TyResult {
    return TyResult { found_in: FoundIn.None, ty: null };
}

def (r: TyResult) is_ok(): bool {
    return r.ty != null;
}

def (r: TyResult) is_error(): bool {
    return r.ty == null;
}

// this performs a normal type lookup, but reports an internal error, if the type as not found
def lookup_builtin(ctx: *ctx.TyCtx, name: str.View): TyResult {
    let ty = ctx.lookup_type(name);
    if ty == null {
        let note = e.simple_note(name);
        report_internal_error_note(str.view_from("builtin type not found"), &note);
        return error();
    }
    return ok(FoundIn.Types, ty);
}

def report_simple(kind: e.ErrorKind, span: span.Span) {
    let error = e.simple_error(kind, span);
    report.print_error(&error);
}

def report_note(kind: e.ErrorKind, span: span.Span, note: *e.ErrorNote) {
    let error = e.simple_error(kind, span);
    error.add_note(note);
    report.print_error(&error);
}

def report_int2(kind: e.ErrorKind, span: span.Span, first: i64, second: i64) {
    let error = e.int_tuple(kind, span, first, second);
    report.print_error(&error);
}

def report_str(kind: e.ErrorKind, span: span.Span, s: str.View) {
    let error = e.string_single(kind, span, s);
    report.print_error(&error);
}

def report_str2(kind: e.ErrorKind, span: span.Span, first: str.View, second: str.View) {
    let error = e.string_tuple(kind, span, first, second);
    report.print_error(&error);
}

def report_str3(kind: e.ErrorKind, span: span.Span, first: str.View, second: str.View, third: str.View) {
    let error = e.string_triple(kind, span, first, second, third);
    report.print_error(&error);
}

def report_internal_error(text: str.View) {
    let msg = str.from("Internal compiler error: ");
    msg.push(text);
    report.print_simple(msg.view());
    msg.free();
}

def report_internal_error_span(text: str.View, span: span.Span) {
    let msg = str.from("Internal compiler error: ");
    msg.push(text);
    report.print_simple_span(msg.view(), span);
    msg.free();
}

def report_internal_error_note(text: str.View, note: *e.ErrorNote) {
    let msg = str.from("Internal compiler error: ");
    msg.push(text);
    report.print_simple_with_note(msg.view(), note);
    msg.free();
}
