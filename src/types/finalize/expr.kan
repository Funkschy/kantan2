import ":std/dbg";
import ":std/str";

import ":util" as _;
import ":error" as e;

import ":source/span";
import ":ast/expr" as _;

import "../util";
import "../ctx" as c;
import "../types" as _;

import "types";

def finalize(ctx: *c.TyCtx, expr: *Expr): Result {
    let result = Result.OK;

    if expr.kind == ExprKind.Assign {
        let assign = &expr.data.assign;
        result = result.or(finalize(ctx, assign.left));
        result = result.or(finalize(ctx, assign.right));

        if result.is_ok() && assign.left.ty.is_unsized() {
            // TODO(#28): implement proper errors for unsized types
            util.report_internal_error_span(str.view_from("unsized bound"), expr.span);
            result = Result.Error;
        }
    }

    if expr.kind == ExprKind.Unary {
        result = result.or(finalize(ctx, expr.data.unary.right));
    }

    if expr.kind == ExprKind.Binary {
        result = result.or(finalize(ctx, expr.data.binary.left));
        result = result.or(finalize(ctx, expr.data.binary.right));
    }

    if expr.kind == ExprKind.Access {
        result = result.or(finalize(ctx, expr.data.access.left));
        result = result.or(finalize(ctx, expr.data.access.ident));
    }

    if expr.kind == ExprKind.Call {
        let call = &expr.data.call;
        result = result.or(finalize(ctx, call.callee));

        let callee_ty = call.callee.ty;
        if callee_ty.kind == TypeKind.Signature {
            let instances = callee_ty.data.signature.func.data.function.instances;
            // this type should already be in the instance, so instead of our newly finalized type,
            // we use the already interned one. This makes sure that all calls to the same function
            // actually have the same type in memory, not just a copy.
            let real_sig_instance = instances.find(callee_ty);
            dbg.assert(real_sig_instance != null, "no signature for call");

            // update the type with the found interned one
            call.callee.ty = real_sig_instance;
        }

        let arg = call.args_head;
        while arg != null {
            result = result.or(finalize(ctx, &arg.value));
            arg = arg.next;
        }

        // update call graph
        ctx.register_call(ctx.current_signature, call.callee.ty);
    }

    if expr.kind == ExprKind.Sizeof {
        result = result.or(types.finalize(ctx, &expr.data.size_of.resolved, expr.data.size_of.tyid.span));
    }

    return result.or(types.finalize(ctx, &expr.ty, expr.span));
}
