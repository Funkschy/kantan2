import ":util" as _;
import ":error" as e;

import ":source/span";

import "../util";
import "../ctx" as c;
import "../types" as _;

def move(ctx: *c.TyCtx, ty: *Type): *Type {
    let memory = ctx.alloc_final_ty();
    // copy the type over
    *memory = *ty;
    ty.kind = TypeKind.Moved;
    ty.data.new_location = memory;
    return memory;
}

// TODO: also add error checks like the scope close
def finalize(ctx: *c.TyCtx, p: **Type, declared_at: span.Span): Result {
    let ty = *p;

    if ty.kind == TypeKind.Error {
        return Result.Error;
    }

    if ty.kind == TypeKind.Moved {
        *p = ty.data.new_location;
        return Result.OK;
    }

    if ty.kind == TypeKind.Variable {
        let result = finalize(ctx, &ty.data.inner, declared_at);
        *p = ty.data.inner;
        ty.kind = TypeKind.Moved;
        ty.data.new_location = ty.data.inner;
        return result;
    }

    if ty.kind == TypeKind.Ptr {
        let result = finalize(ctx, &ty.data.ptr_to, declared_at);
        *p = move(ctx, ty);
        return result;
    }

    if ty.kind == TypeKind.AnyInt {
        ty.kind = TypeKind.Moved;
        ty.data.new_location = ctx.final_i32_ty;
        *p = ctx.final_i32_ty;
        return Result.OK;
    }

    if ty.kind == TypeKind.AnyFloat {
        ty.kind = TypeKind.Moved;
        ty.data.new_location = ctx.final_f32_ty;
        *p = ctx.final_f32_ty;
        return Result.OK;
    }

    if ty.kind == TypeKind.Any {
        util.report_simple(e.ErrorKind.CouldNotInfer, declared_at);
        return Result.Error;
    }

    if ty.kind == TypeKind.Function {
        let f = &ty.data.function;

        let had_errors = false;
        let current = f.params_head;
        while current != null {
            had_errors |= finalize(ctx, &current.value, declared_at).is_error();
            current = current.next;
        }
        had_errors |= finalize(ctx, &f.ret, declared_at).is_error();

        if had_errors {
            return Result.Error;
        }
    }

    *p = move(ctx, ty);
    return Result.OK;
}
