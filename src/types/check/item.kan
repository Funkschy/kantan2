import ":std/dbg";
import ":std/str";
import ":util" as _;
import ":ast/item" as _;

import ":error" as e;

import "../util";
import "../ctx" as _;
import "../types" as _;

import "stmt" as tychk;

def check(ctx: *TyCtx, item: *Item): Result {
    if item.kind == ItemKind.FuncDef {
        let func_ty = ctx.lookup_value(item.name.as_view());
        if func_ty == null {
            util.report_str(e.ErrorKind.UndeclaredType, item.name.span, item.name.as_view());
            return Result.Error;
        }

        let is_template = func_ty.is_template();
        if is_template && ctx.current_pass != Pass.Templates {
            return Result.OK;
        } else if !is_template && ctx.current_pass != Pass.Functions {
            return Result.OK;
        }

        let i: usize = 0;
        let instance = func_ty;
        if is_template {
            instance = null;
            i = func_ty.data.template.instances.checked;

            if i < func_ty.data.template.instances.len() {
                ctx.had_unchecked_templates = true;
                instance = func_ty.data.template.instances.get(i);
            }
        }

        let result = Result.OK;
        while instance != null {
            ctx.open_scope();
            // bind params
            let param = instance.data.function.params_head;
            while param != null {
                dbg.assert(param.name.kind == NameKind.Ident, "expected ident, not str.View");
                let name = &param.name.data.ident;
                ctx.bind_value_public(name.as_view(), param.value, &name.span);
                param = param.next;
            }

            ctx.current_function = instance;
            result = result.or(tychk.check(ctx, &item.data.func_def.block_stmt));
            ctx.close_scope();

            i += 1;
            if is_template && i < func_ty.data.template.instances.len() {
                instance = func_ty.data.template.instances.get(i);
            } else {
                instance = null;
            }
        }

        if is_template {
            func_ty.data.template.instances.checked = i;
        }

        return result;
    }

    dbg.assert(false, "Unhandled ItemKind");
    return Result.Error;
}
