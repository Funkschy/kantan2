import "std/str";
import "cdeps";

import "source/span";

let error_strings: [18]string = undefined;

type ErrorKind enum {
    // import errors
    CouldNotImport,
    SelfImport,
    // parse errors
    ExpectedButGot,
    CouldNotScanToken,
    ExpectedExpression,
    ExpectedOperator,
    EmptyGenericsList,
    CouldNotParseStmt, // this is the most generic error msg by design
    // type errors
    UndeclaredIdentifier,
    CouldNotInfer,
    WrongType,
    DerefNonPtr,
    NegateNonNumber,
    UndeclaredType,
    InvalidBinaryOperator,
    WrongNumberOfArgs,
    CallingNonFunction,
    SizeofUnsizedType
}

type ErrorDataKind enum {
    NoAdditionalData,
    IntTuple,
    StringSingle,
    StringTuple,
    StringTriple
}

type Error struct {
    kind: ErrorKind,
    data_kind: ErrorDataKind,
    note: *ErrorNote,
    span: span.Span,
    data: ErrorData
}

type ErrorData union {
    int_tuple: [2]i64,
    string_single: str.View,
    string_tuple: [2]str.View,
    string_triple: [3]str.View
}

def (e: *Error) fmt_string(): string {
    return error_strings[e.kind as i32];
}

def (e: *Error) to_string(): str.String {
    let len: usize = 0;
    let value: string = null;

    if e.data_kind == ErrorDataKind.NoAdditionalData {
        return str.from(e.fmt_string());
    }

    if e.data_kind == ErrorDataKind.StringSingle {
        let str = e.data.string_single;
        value = cdeps.l_format_str(
            &len,
            e.fmt_string(),
            str.len, str.data
        );
    } else if e.data_kind == ErrorDataKind.StringTuple {
        let first = e.data.string_tuple[0];
        let second = e.data.string_tuple[1];
        value = cdeps.l_format_str(
            &len,
            e.fmt_string(),
            first.len, first.data,
            second.len, second.data
        );
    } else if e.data_kind == ErrorDataKind.IntTuple {
        let first = e.data.int_tuple[0];
        let second = e.data.int_tuple[1];
        value = cdeps.l_format_str(&len, e.fmt_string(), first, second);
    } else if e.data_kind == ErrorDataKind.StringTriple {
        let first = e.data.string_triple[0];
        let second = e.data.string_triple[1];
        let third = e.data.string_triple[2];
        value = cdeps.l_format_str(
            &len,
            e.fmt_string(),
            first.len, first.data,
            second.len, second.data,
            third.len, third.data
        );
    }

    return str.move_l(value, len);
}

def (e: *Error) add_note(note: *ErrorNote) {
    if e.note == null {
        e.note = note;
        return;
    }

    let last = e.note;
    while last.next != null {
        last = last.next;
    }

    last.next = note;
}

def simple_error(kind: ErrorKind, span: span.Span): Error {
    return Error {
        kind: kind,
        data_kind: ErrorDataKind.NoAdditionalData,
        note: null,
        span: span,
        data: undefined
    };
}

def int_tuple(kind: ErrorKind, span: span.Span, first: i64, second: i64): Error {
    let data: ErrorData = undefined;
    data.int_tuple[0] = first;
    data.int_tuple[1] = second;

    return Error {
        kind: kind,
        data_kind: ErrorDataKind.IntTuple,
        note: null,
        span: span,
        data: data
    };
}

def string_single(kind: ErrorKind, span: span.Span, str: str.View): Error {
    return Error {
        kind: kind,
        data_kind: ErrorDataKind.StringSingle,
        note: null,
        span: span,
        data: ErrorData { string_single: str }
    };
}

def string_tuple(kind: ErrorKind, span: span.Span, first: str.View, second: str.View): Error {
    let data: ErrorData = undefined;
    data.string_tuple[0] = first;
    data.string_tuple[1] = second;

    return Error {
        kind: kind,
        data_kind: ErrorDataKind.StringTuple,
        note: null,
        span: span,
        data: data
    };
}

def string_triple(kind: ErrorKind, span: span.Span, first: str.View, second: str.View, third: str.View): Error {
    let data: ErrorData = undefined;
    data.string_triple[0] = first;
    data.string_triple[1] = second;
    data.string_triple[2] = third;

    return Error {
        kind: kind,
        data_kind: ErrorDataKind.StringTriple,
        note: null,
        span: span,
        data: data
    };
}

type StringTuple struct {
    first: str.View,
    second: str.View
}

def init() {
    error_strings[ErrorKind.CouldNotImport as i32]  = "Invalid import: %.*s";
    error_strings[ErrorKind.SelfImport as i32]      = "Modules cannot import themselves";
    // parse errors
    error_strings[ErrorKind.ExpectedButGot as i32]      = "Expected '%.*s', but got '%.*s'";
    error_strings[ErrorKind.CouldNotScanToken as i32]   = "Could not scan token";
    error_strings[ErrorKind.ExpectedExpression as i32]  = "Expected expression";
    error_strings[ErrorKind.ExpectedOperator as i32]  = "Expected an operator";
    error_strings[ErrorKind.EmptyGenericsList as i32]  = "An empty list of generics is invalid";
    error_strings[ErrorKind.CouldNotParseStmt as i32]  = "Could not parse statement";
    // type errors
    error_strings[ErrorKind.UndeclaredIdentifier as i32]  = "No identifier '%.*s' in scope";
    error_strings[ErrorKind.CouldNotInfer as i32]  = "Could not infer type";
    error_strings[ErrorKind.WrongType as i32]  = "Expected '%.*s', but got '%.*s'";
    error_strings[ErrorKind.DerefNonPtr as i32]  = "Trying to deref '%.*s', which is not a pointer";
    error_strings[ErrorKind.NegateNonNumber as i32]  = "Trying to negate '%.*s', which is not a number";
    error_strings[ErrorKind.UndeclaredType as i32]  = "No type '%.*s' in scope";
    error_strings[ErrorKind.InvalidBinaryOperator as i32]  = "Invalid operator '%.*s' for types '%.*s' and '%.*s'";
    error_strings[ErrorKind.WrongNumberOfArgs as i32]  = "Expected %lld arguments, but got %lld";
    error_strings[ErrorKind.CallingNonFunction as i32]  = "The type '%.*s' is not callable";
    error_strings[ErrorKind.SizeofUnsizedType as i32]  = "The type '%.*s' is unsized";
}

// A Linked List of additional information regarding an error, e.g. the location of usage
// the function which adds the notes is also responsible for freeing them
type ErrorNote struct {
    // if this is true, it means that the span field must be ignored, because this is just a simple
    // string message without any associated source file or position
    without_position: bool,
    message: str.View,
    span: span.Span,
    next: *ErrorNote
}

def note(message: str.View, span: span.Span): ErrorNote {
    return ErrorNote { without_position: false, message: message, span: span, next: null };
}

def simple_note(message: str.View): ErrorNote {
    return ErrorNote { without_position: true, message: message, span: span.empty(), next: null };
}
