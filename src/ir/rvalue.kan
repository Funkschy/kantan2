import ":std/str";
import ":std/dbg";

import ":cdeps";

import ":types/types" as ty;

import "ir";
import "memory";

type ExpressionKind enum {
    Use, Ref, Cast, Binary, Unary
}

type Expression struct {
    kind: ExpressionKind,
    data: ExpressionData
}

type ExpressionData union {
    use: Operand,
    ref: memory.Location,
    cast: Cast,
    binary: BinaryOperation,
    null_value: *ty.Type,
    undef_value: *ty.Type
}

def expr_ref(location: memory.Location): Expression {
    return Expression {
        kind: ExpressionKind.Ref,
        data: ExpressionData { ref: location }
    };
}

def expr_copy(location: memory.Location): Expression {
    return expr_use(copy(location));
}

def expr_const(constant: Constant): Expression {
    return expr_use(const(constant));
}

def expr_use(operand: Operand): Expression {
    return Expression {
        kind: ExpressionKind.Use,
        data: ExpressionData { use: operand }
    };
}

type OperandKind enum {
    Copy, Constant
}

type Operand struct {
    kind: OperandKind,
    data: OperandData
}

type OperandData union {
    copy: memory.Location,
    constant: Constant
}

def copy(location: memory.Location): Operand {
    return Operand {
        kind: OperandKind.Copy,
        data: OperandData { copy: location }
    };
}

def const(constant: Constant): Operand {
    return Operand {
        kind: OperandKind.Constant,
        data: OperandData { constant: constant }
    };
}

type OperandNode struct {
    value: Operand,
    next: *OperandNode
}

type CastKind enum {
    Pointer, Int
}

type Cast struct {
    kind: CastKind,
    operand: Operand,
    into: *ty.Type
}

type BinaryKind enum {
    Add = 1,
    Sub,
    Mul,
    Div,
    Mod,
    BitAnd,
    BitOr,
    BitXor,

    ST,
    SE,
    GT,
    GE,
    EQ,
    NE,

    BoolAnd,
    BoolOr,

    LShift,
    RShift,

    AddScalar,
    SubScalar
}

type BinaryOperation struct {
    kind: BinaryKind,
    left: Operand,
    right: Operand
}

type UnaryKind enum {
    Not = 1,
    NumNeg
}

type UnaryOperation struct {
    kind: UnaryKind,
    operand: Operand
}

type ConstantKind enum {
    Null,
    Undefined,
    Char,
    Int,
    Float,
    Function // a function that is called by name, so the dispatch is known at compile time
}

type Constant struct {
    kind: ConstantKind,
    ty: *ty.Type,
    data: ConstantData
}

type ConstantData union {
    // null/undefined do not need any additional data
    char: i8,
    int: u64,
    float: f64,
    function: *ir.Function
}

def const_undefined(ty: *ty.Type): Constant {
    return Constant {
        kind: ConstantKind.Undefined,
        ty: ty,
        data: undefined
    };
}

def const_null(ty: *ty.Type): Constant {
    return Constant {
        kind: ConstantKind.Null,
        ty: ty,
        data: undefined
    };
}

def const_int(ty: *ty.Type, value: u64): Constant {
    return Constant {
        kind: ConstantKind.Int,
        ty: ty,
        data: ConstantData { int: value }
    };
}

def const_float(ty: *ty.Type, value: f64): Constant {
    return Constant {
        kind: ConstantKind.Float,
        ty: ty,
        data: ConstantData { float: value }
    };
}

def const_char(ty: *ty.Type, value: i8): Constant {
    return Constant {
        kind: ConstantKind.Char,
        ty: ty,
        data: ConstantData { char: value }
    };
}

def const_function(function: *ir.Function): Constant {
    return Constant {
        kind: ConstantKind.Function,
        ty: function.ty,
        data: ConstantData { function: function }
    };
}

// json generation stuff

def (e: *Expression) to_json(): str.String {
    if e.kind == ExpressionKind.Use {
        let json = str.from("{\"kind\": \"use\", \"operand\": ");
        defer json.push(str.view("}", 1));

        let operand = e.data.use.to_json();
        json.push(operand.view());
        operand.free();

        return json;
    }

    if e.kind == ExpressionKind.Ref {
        let json = str.from("{\"kind\": \"ref\"");
        defer json.push(str.view("}", 1));
        return json;
    }

    dbg.assert(false, "unhandled expression kind");
    return undefined;
}

def (op: *Operand) to_json(): str.String {
    let json = str.from("{\"kind\": ");
    defer json.push(str.view("}", 1));

    if op.kind == OperandKind.Copy {
        json.push(str.view_from("\"copy\", \"location\": "));
        let location = op.data.copy.to_json();
        json.push(location.view());
        location.free();
    } else {
        let c = &op.data.constant;
        json.push(str.view_from("\"constant\", \"type\": \""));
        let ty_s = c.ty.to_string();
        json.push(ty_s.view());
        ty_s.free();
        json.push(str.view_from("\", \"value\": "));
        let value = c.to_json();
        json.push(value.view());
        value.free();
    }

    return json;
}

def (c: *Constant) to_json(): str.String {
    if c.kind == ConstantKind.Undefined {
        let json = str.from("{\"kind\": \"undefined\"");
        defer json.push(str.view("}", 1));
        return json;
    }

    if c.kind == ConstantKind.Null {
        let json = str.from("{\"kind\": \"null\"");
        defer json.push(str.view("}", 1));
        return json;
    }

    if c.kind == ConstantKind.Char {
        let json = str.from("{\"kind\": \"char\", \"value\": ");
        defer json.push(str.view("}", 1));

        let value = str.i64_to_string(c.data.char as i64);
        json.push(value.view());
        value.free();

        return json;
    }

    if c.kind == ConstantKind.Int {
        let json = str.from("{\"kind\": \"int\", \"value\": ");
        defer json.push(str.view("}", 1));

        let value = str.i64_to_string(c.data.int);
        json.push(value.view());
        value.free();

        return json;
    }

    if c.kind == ConstantKind.Float {
        let json = str.from("{\"kind\": \"float\", \"value\": ");
        defer json.push(str.view("}", 1));

        let value = cdeps.f64_to_string(c.data.float);
        json.push(value.view());
        value.free();

        return json;
    }

    if c.kind == ConstantKind.Function {
        let json = str.from("{\"kind\": \"function\"");
        defer json.push(str.view("}", 1));
        json.push(str.view_from(", \"name\": \""));
        json.push(c.data.function.original_name.as_view());
        json.push(str.view("\"", 1));
        return json;
    }

    dbg.assert(false, "unhandled constant kind");
    return undefined;
}
