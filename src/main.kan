import "io";
import "std/str";
import "std/ptrvec";

import "cli/opt";
import "cli/report";
import "cli/config" as conf;

import "util" as _;
import "compiler" as _;

// for table initialization
import "target/parse" as ptarget;
import "ast/token";
import "error";

type ReturnCode enum {
    OK,
    NoInputFiles,
    CliOptionError,
    InvalidInput
}

def main(argc: i32, argv: *string): ReturnCode {
    if argc <= 1 {
        report.print_simple(str.view("no input files", 14));
        return ReturnCode.NoInputFiles;
    }

    init_tables();

    let opts = opt.parse(argc, argv);
    let files = &opts.files;
    defer files.free();

    if opts.had_errors {
        return ReturnCode.CliOptionError;
    } else if opts.exit_immediately {
        return ReturnCode.OK;
    }

    let config: conf.Config = undefined;
    if conf.from_options(&opts, &config).is_error() {
        return ReturnCode.CliOptionError;
    }
    defer config.free();

    let compiler = compiler(&config);
    defer compiler.free();

    // this is needed for mi mode, where errors are printed as json
    report.init(config.error_output_format, &compiler.modmap);
    defer report.finish();

    if opts.dump_config {
        io.printf("\"config\": ");
        defer io.printf(",\n");

        let json = config.to_json();
        io.printf("%s", json.cstring());
        json.free();
    }

    if config.files.len < 1 {
        report.print_simple(str.view("no kantan files", 15));
        return ReturnCode.NoInputFiles;
    }

    if compiler.read_files().is_error() {
        return ReturnCode.InvalidInput;
    }

    if compiler.resolve_imports().is_error() {
        return ReturnCode.InvalidInput;
    }

    if compiler.parse().is_error() {
        return ReturnCode.InvalidInput;
    }

    if config.quit_after_parsing {
        if opts.dump_ast {
            dump_ast(&compiler);
        }
        return ReturnCode.OK;
    }

    if compiler.resolve_types().is_error() {
        return ReturnCode.InvalidInput;
    }

    // TODO(#41): integrate this into the program in more appropriate manner
    if opts.dump_type_graph {
        let g = &compiler.type_graph;

        io.printf("\"type-graph\": ");
        defer io.printf(",\n");

        let json = g.to_json(str.view_from("type graph"));
        io.printf("%s", json.cstring());
        json.free();
    }

    if compiler.infer_and_check_types().is_error() {
        return ReturnCode.InvalidInput;
    }

    if opts.dump_ast {
        dump_ast(&compiler);
    }

    {
        import "ir/ir";
        import "ir/compile/item" as ir_item;
        import "ir/compile/ctx" as ir_ctx;

        let ctx = ir_ctx.create();
        defer ctx.free();

        let result = Result.OK;
        for let i: usize = 0; i < compiler.modmap.len(); i += 1 {
            let mod = compiler.modmap.at(i);

            for let j: usize = 0; j < mod.num_items(); j += 1 {
                let item = mod.item_at(j);
                result = result.or(ir_item.compile(&ctx, item));
            }
        }

        if result.is_error() {
            return ReturnCode.InvalidInput;
        }

        if opts.dump_ir {
            io.printf("\"ir\": [");
            defer io.printf("],\n");
            for let i: usize = 0; i < ctx.functions.len; i += 1 {
                if i > 0 {
                    io.printf(", ");
                }

                let f = ctx.functions.get_ptr(i) as *ir.Function;
                let json = f.to_json();
                io.printf("%s\n", json.cstring());
                json.free();
            }
        }
    }

    return ReturnCode.OK;
}

def dump_ast(compiler: *Compiler) {
    // TODO(#12): integrate this into the program in more appropriate manner
    io.printf("\"modules\": [");
    defer io.printf("],\n");

    for let i: usize = 0; i < compiler.modmap.len(); i += 1 {
        if i > 0 {
            io.printf(", ");
        }

        let mod = compiler.modmap.at(i);
        let json = mod.to_json();
        io.printf("%s\n", json.cstring());
        json.free();
    }
}

// Initialize global arrays
// this function should be removed, as soon as we can declare global array literals
def init_tables() {
    ptarget.init();
    token.init();
    error.init();
}
