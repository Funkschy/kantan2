import "io";
import "std/str";
import "std/ptrvec";

import "cli/opt";
import "cli/report";
import "cli/config" as conf;

import "compiler" as _;

// for table initialization
import "target/parse" as ptarget;
import "ast/token";
import "error";

type ReturnCode enum {
    OK,
    NoInputFiles,
    CliOptionError,
    InvalidInput
}

def main(argc: i32, argv: *string): ReturnCode {
    if argc <= 1 {
        report.print_simple(str.view("no input files", 14));
        return ReturnCode.NoInputFiles;
    }

    init_tables();

    let opts = opt.parse(argc, argv);
    let files = &opts.files;
    defer files.free();

    if opts.had_errors {
        return ReturnCode.CliOptionError;
    } else if opts.exit_immediately {
        return ReturnCode.OK;
    }

    let config: conf.Config = undefined;
    if conf.from_options(&opts, &config).is_error() {
        return ReturnCode.CliOptionError;
    }
    defer config.free();

    let compiler = compiler(&config);
    defer compiler.free();

    // this is needed for mi mode, where errors are printed as json
    report.init(config.error_output_format, &compiler.modmap);
    defer report.finish();

    if opts.dump_config {
        io.printf("\"config\": ");
        defer io.printf(",\n");

        let json = config.to_json();
        io.printf("%s", json.cstring());
        json.free();
    }

    if config.files.len < 1 {
        report.print_simple(str.view("no kantan files", 15));
        return ReturnCode.NoInputFiles;
    }

    compiler.read_files();
    compiler.resolve_imports();
    if compiler.parse().is_error() {
        return ReturnCode.InvalidInput;
    }

    if config.quit_after_parsing {
        if opts.dump_ast {
            dump_ast(&compiler);
        }
        return ReturnCode.OK;
    }

    // TODO(#4): this is debug code
    import "types/types";
    import "memory/arena";

    let ty_arena = arena.typed(sizeof types.Type);
    defer ty_arena.free();
    let node_arena = arena.typed(sizeof types.TypeNode);
    defer node_arena.free();

    {
        import "types/ctx";
        import "types/check/item" as i_check;
        import "types/finalize/item" as i_finalize;
        import "types/resolve/functions" as resolve_functions;
        import "types/info";
        import "types/primitive";

        let ctx = ctx.create(&ty_arena, &node_arena);
        defer ctx.free();

        if opts.dump_type_graph {
            // TODO(#35): remove this test code
            import "types/graph";
            let g = graph.type_graph();
            defer g.free();

            io.printf("\"type-graph\": ");
            defer io.printf(",\n");

            let json = g.to_json(str.view_from("stupid graph"));
            io.printf("%s", json.cstring());
            json.free();
        }

        let had_errors = false;
        for let i: usize = 0; i < compiler.modmap.len(); i += 1 {
            let mod = compiler.modmap.at(i);
            ctx.open_scope();
            defer ctx.close_scope();

            for let j: usize = 0; j < mod.num_items(); j += 1 {
                let item = mod.item_at(j);
                if resolve_functions.resolve(&ctx, item).is_error() {
                    had_errors = true;
                }
            }

            for let j: usize = 0; j < mod.num_items(); j += 1 {
                let item = mod.item_at(j);
                if i_check.check(&ctx, item).is_error() {
                    had_errors = true;
                }
            }
        }

        if had_errors {
            return ReturnCode.InvalidInput;
        }

        // FIXME: add missing branches
        // TODO: we need to mark i32 and f32 as moved before finalizing
        let had_errors = false;
        for let i: usize = 0; i < compiler.modmap.len(); i += 1 {
            let mod = compiler.modmap.at(i);

            for let j: usize = 0; j < mod.num_items(); j += 1 {
                let item = mod.item_at(j);
                if i_finalize.finalize(&ctx, item).is_error() {
                    had_errors = true;
                }
            }
        }

        if had_errors {
            return ReturnCode.InvalidInput;
        }
    }

    if opts.dump_ast {
        dump_ast(&compiler);
    }

    return ReturnCode.OK;
}

def dump_ast(compiler: *Compiler) {
    // TODO(#12): integrate this into the program in more appropriate manner
    io.printf("\"modules\": [");
    defer io.printf("],\n");

    for let i: usize = 0; i < compiler.modmap.len(); i += 1 {
        if i > 0 {
            io.printf(", ");
        }

        let mod = compiler.modmap.at(i);
        let json = mod.to_json();
        io.printf("%s\n", json.cstring());
        json.free();
    }
}

// Initialize global arrays
// this function should be removed, as soon as we can declare global array literals
def init_tables() {
    ptarget.init();
    token.init();
    error.init();
}
