import "../std/str";
import "../std/dbg";
import "../std/map";
import "../std/vmap";
import "../std/files/path";

import "../ast/mod";
import "../ast/lexer" as lex;
import "../util";

import "file" as sf; // source/file
import "span";

type ModuleInfo struct {
    was_imported: bool,
    has_lexer: bool,
    module: *mod.Module,
    // we safe the lexer if we want to resume the parsing at a later point
    lexer: lex.Lexer
}

def info(module: *mod.Module): ModuleInfo {
    return ModuleInfo {
        was_imported: false,
        has_lexer: false,
        module: module,
        lexer: undefined
    };
}

def (i: *ModuleInfo) key(): map.Key {
    return util.key_from_path(i.module.file.absolute_path());
}

type Map struct {
    // this has to be a vmap, since we want to mutate it, while iterating. That is not something
    // that the normal map.Map supports, but the vmap can be iterated in order, while appending
    // further modules at the end
    modules: vmap.Map // vmap.Map[path.Path, ModuleInfo]
}

def filemap(): Map {
    return Map { modules: vmap.create(sizeof ModuleInfo) };
}

// add a file the the filemap. The relative_path is resolved in relation to the current working
// directory (cwd)
// if the file could be read, the file will inserted into the map
// if an error occured during file reading, the file will not be inserted
// The dest_mod.file is set in any case and must be checked for file.is_error() by the caller
def (m: *Map) add_file(cwd: path.Path, relative_path: path.Path, dest_mod: *mod.Module) {
    let file = sf.read_kantan_file(cwd, relative_path);
    dest_mod.file = file;

    if !file.is_error() {
        let info = info(dest_mod);
        m.modules.insert(info.key(), &info as *void);
    }
}

def (m: *Map) get_mod_from_span(span: span.Span): *mod.Module {
    for let i: usize = 0; i < m.modules.len(); i += 1 {
        let mod = m.at(i);
        let code = mod.file.code();
        if span.start >= code.data && span.end <= code.data + code.len {
            return mod;
        }
    }

    return null;
}

def (m: *Map) len(): usize {
    return m.modules.len();
}

def (m: *Map) mark_imported(path: path.Path) {
    let info = m.modules.get_ptr(util.key_from_path(path)) as *ModuleInfo;
    dbg.assert(info != null, "trying to mark non existing module");
    info.was_imported = true;
}

def (m: *Map) get_mod(path: path.Path): *mod.Module {
    let info = m.modules.get_ptr(util.key_from_path(path)) as *ModuleInfo;
    if info == null {
        return null;
    }
    return info.module;
}

def (m: *Map) at(idx: usize): *mod.Module {
    let info = m.info_at(idx);
    if info == null {
        return null;
    }
    return info.module;
}

def (m: *Map) info_at(idx: usize): *ModuleInfo {
    return m.modules.get_ptr_idx(idx) as *ModuleInfo;
}

def (m: *Map) save_state_for(idx: usize, lexer: lex.Lexer) {
    let info = m.info_at(idx);
    dbg.assert(info != null, "trying to safe state for non existing module");
    info.has_lexer = true;
    info.lexer = lexer;
}

def (m: *Map) free() {
    for let i: usize = 0; i < m.modules.len(); i += 1 {
        m.at(i).free();
    }
    m.modules.free();
}
